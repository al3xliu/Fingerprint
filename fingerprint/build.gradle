apply plugin: 'com.android.library'

ext {
    bintrayRepo = 'maven'
    bintrayName = 'fingerprint'

    publishedGroupId = 'me.aflak.libraries'
    libraryName = 'Fingerprint'
    artifact = 'fingerprint'

    libraryDescription = 'Android library that simplifies fingerprint authentications.'

    siteUrl = 'https://github.com/omaraflak/Fingerprint'
    gitUrl = 'https://github.com/omaraflak/Fingerprint.git'

    libraryVersion = '2.5.3'

    developerId = 'omaflak'
    developerName = 'Omar Aflak'
    developerEmail = 'aflakomar@gmail.com'

    licenseName = 'The MIT License'
    licenseUrl = 'https://opensource.org/licenses/MIT'
    allLicenses = ["MIT"]
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 37
        versionName project.ext.libraryVersion

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        checkTypes {
            javaCompileOptions.annotationProcessorOptions.
                    classNames.add("org.checkerframework.checker.crypto.CryptoChecker")
            // Uncomment to do the strongboxbacked checking.
            // javaCompileOptions.annotationProcessorOptions.arguments.put("lint", "strongboxbacked")
            javaCompileOptions.annotationProcessorOptions.arguments.put("stubs", "hardwarebacked.astub")
        }
    }
    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }
}

repositories {
    mavenLocal()
}

configurations {
    checkerFrameworkAnnotatedJDK {
        description = 'a copy of JDK classes with Checker Framework type qualifiers inserted'
    }
    errorproneJavac {
        description = 'required to run the Checker Framework.'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'

    ext.checkerFrameworkVersion = '3.1.1'
    annotationProcessor "org.checkerframework:crypto-checker:0.1-SNAPSHOT"
    implementation files("../../../../crypto-checker-qual-android/build/libs/crypto-checker-qual-android.jar")
    checkerFrameworkAnnotatedJDK files("../../../../jdk8.jar")
    compileOnly "org.checkerframework:checker-qual-android:${checkerFrameworkVersion}"
    errorproneJavac 'com.google.errorprone:javac:9+181-r4173-1'
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile).all { compile ->
        if (compile.name.contains("CheckTypes")) {
            compile.options.compilerArgs += [
                    "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}"
            ]
            options.fork = true
            options.forkOptions.jvmArgs += ["-Xbootclasspath/p:${configurations.errorproneJavac.asPath}"]
        }
    }
}

apply from: 'https://raw.githubusercontent.com/omaflak/Bintray/master/install.gradle'
apply from: 'https://raw.githubusercontent.com/omaflak/Bintray/master/bintray.gradle'